plugins {
    id 'org.jenkins-ci.jpi' version '0.42.0'
    id 'maven-publish'
    id 'signing'
}

description = 'Galasa Jenkins Core Plugin'

group = galasaGroup

// If a subproject/bundle holds a different version to the root project,
// <bundle>/gradle.properties should be given the property "bundleVersion"
// e.g. bundleVersion=0.1.1
if (project.hasProperty('bundleVersion')){
    version = bundleVersion
} else {
    version = galasaVersion
}

// JDK compatibility
sourceCompatibility = galasaSourceCompatibility
targetCompatibility = galasaTargetCompatibility

ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

def mavenrepo = System.getenv('MAVEN_REPO') ?: "https://nexus.galasa.dev/repository/maven-development"

tasks.withType(Sign) {
    onlyIf { isReleaseVersion }
}

tasks.withType(PublishToMavenRepository) {
    onlyIf { jar.didWork }
}

java {
    withJavadocJar()
    withSourcesJar()
}

tasks.withType(Javadoc) {
    failOnError = false
}

jenkinsPlugin {
    // Must appear before repositories stanza
    jenkinsVersion = '2.204'
    displayName = description
    disabledTestInjection = false
}

repositories {
    maven {
        url mavenrepo
    }
    maven {
        url 'https://repo.jenkins-ci.org/public/'
    }
    mavenCentral()
}


dependencies{
    implementation ('dev.galasa:dev.galasa:0.15.0-SNAPSHOT')
    implementation ('dev.galasa:dev.galasa.framework:0.15.0-SNAPSHOT')
    implementation ('com.google.code.gson:gson:2.8.5')
    implementation ('io.swagger.core.v3:swagger-annotations:2.0.8')
    implementation ('org.apache.httpcomponents:httpclient:4.5.8')
    implementation ('org.apache.httpcomponents:fluent-hc:4.5.8')
    implementation ('javax.servlet:javax.servlet-api:3.1.0')
    implementation ('org.jenkins-ci.plugins:credentials:2.1.16')
    implementation ('org.jenkins-ci.main:jenkins-core:2.204')
    implementation ('commons-io:commons-io:2.8.0')


    testImplementation ('org.jvnet.hudson.main:hudson-test-framework:2.2.1')
}


publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            
            pom {
                name = 'Galasa Testers Programmer Interface (TPI)'
                url = 'https://galasa.dev'
                licenses {
                    license {
                        name = 'Eclipse Public License - v 2.0'
                        url = 'https://www.eclipse.org/legal/epl-2.0/t'
                    }
                }
                developers {
                    developer {
                        name = 'Michael Baylis'
                        email = 'Michael.Baylis@uk.ibm.com'
                        organization = 'IBM'
                        organizationUrl = 'https://www.ibm.com'
                    }
                    developer {
                        name = 'William Yates'
                        email = 'wyates@uk.ibm.com'
                        organization = 'IBM'
                        organizationUrl = 'https://www.ibm.com'
                    }
                    developer {
                        name = 'David Roberts'
                        email = 'david.roberts@uk.ibm.com'
                        organization = 'IBM'
                        organizationUrl = 'https://www.ibm.com'
                    }
                    developer {
                        name = 'James Davies'
                        email = 'james.davies@ibm.com'
                        organization = 'IBM'
                        organizationUrl = 'https://www.ibm.com'
                    }
                    developer {
                        name = 'Matthew Chivers'
                        email = 'matthew.chivers@ibm.com'
                        organization = 'IBM'
                        organizationUrl = 'https://www.ibm.com'
                    }
                    developer {
                        name = 'Charlie Meek'
                        email = 'charlie.meek@ibm.com'
                        organization = 'IBM'
                        organizationUrl = 'https://www.ibm.com'
                    }
                    developer {
                        name = 'Diana Slepikaite'
                        email = 'diana.slepikaite@ibm.com'
                        organization = 'IBM'
                        organizationUrl = 'https://www.ibm.com'
                    }
                }
                scm {
                    connection = 'scm:git:git:://github.com/galasa-dev/framework'
                    developerConnection = 'scm:git:git:://github.com/galasa-dev/framework'
                    url = 'https://github.com/galasa-dev/framework'
                }
                issueManagement {
                    system = 'GitHub'
                    url = 'https://github.com/galasa-dev/projectmanagement/issues'
                }
            }
        }
    }
    repositories {
        maven {
            credentials {
                username System.getenv('MAVENUSERNAME')
                password System.getenv('MAVENPASSWORD')
            }
            url = mavenrepo
        }
    }
}

signing {
    useGpgCmd() 
    sign publishing.publications.maven 
}
    